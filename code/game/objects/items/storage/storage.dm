/obj/item/storage
	name = "storage"
	w_class = WEIGHT_CLASS_NORMAL
	var/rummage_if_nodrop = TRUE
	var/component_type = /datum/component/storage/concrete
	var/list/populate_contents = list()
	obj_flags = CAN_BE_HIT
	var/preload = FALSE

/obj/item/storage/get_mechanics_examine(mob/user)
	. = ..()
	. += span_info("Left-click with an empty hand to open its inventory. Alternatively, left-clicking with an item will attempt to store it inside; pouches, belts, and packs have different limits to what they can carry.")
	. += span_info("Click-drag onto an empty hand's slot to take it off. You can take pouches, belts, and packs off other people by click-dragging them onto yourself and selecting the item-in-question on the resulting interface.")
	. += span_info("While hovering over an open inventory slot with an item in your active hand, right-clicking will rotate the item's 'space'. As most inventories use a grid system, doing this can let you further optimize your storage.")

/obj/item/storage/get_dumping_location(obj/item/storage/source, mob/user)
	return src

/obj/item/storage/Initialize(mapload)
	. = ..()
	AddComponent(component_type)
	PopulateContents()

	for (var/obj/item/item in src)
		item.item_flags |= IN_STORAGE

/obj/item/storage/AllowDrop()
	return FALSE

/obj/item/storage/contents_explosion(severity, target)
	for(var/atom/A in contents)
		A.ex_act(severity, target)
		CHECK_TICK

/obj/item/storage/canStrip(mob/who)
	. = ..()
	if(!. && rummage_if_nodrop)
		return TRUE

/obj/item/storage/doStrip(mob/who)
	if(HAS_TRAIT(src, TRAIT_NODROP) && rummage_if_nodrop)
		var/datum/component/storage/CP = GetComponent(/datum/component/storage)
		CP.do_quick_empty()
		return TRUE
	return ..()

/obj/item/storage/contents_explosion(severity, target)
//Cyberboss says: "USE THIS TO FILL IT, NOT INITIALIZE OR NEW"

/obj/item/storage/proc/PopulateContents()
	for(var/path in populate_contents)
		var/obj/item/new_item = new path(loc)
		if(!SEND_SIGNAL(src, COMSIG_TRY_STORAGE_INSERT, new_item, null, TRUE, TRUE))
			new_item.inventory_flip(null, TRUE)
			if(!SEND_SIGNAL(src, COMSIG_TRY_STORAGE_INSERT, new_item, null, TRUE, TRUE))

				qdel(new_item)

/obj/item/storage/proc/emptyStorage()
	var/datum/component/storage/ST = GetComponent(/datum/component/storage)
	if(ST)
		ST.do_quick_empty()

/obj/item/storage/can_craft_with()
	if(contents.len)
		return FALSE
	return ..()


/// Returns a list of object types to be preloaded by our code
/// I'll say it again, be very careful with this. We only need it for a few things
/// Don't do anything stupid, please
/obj/item/storage/proc/get_types_to_preload()
	return
