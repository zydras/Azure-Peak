/obj/structure/chair/bench
	name = "bench"
	desc = "A stouter, wider chair. It doesn't look terribly comfortable."
	icon_state = "bench"
	icon = 'icons/roguetown/misc/structure.dmi'
	buildstackamount = 1
	item_chair = null
	destroy_sound = 'sound/combat/hits/onwood/destroyfurniture.ogg'
	attacked_sound = "woodimpact"
	sleepy = 0.5
//	pixel_y = 10
	layer = OBJ_LAYER
	hidingspot = TRUE
	var/mob/living/hiddenguy = null // So we can find them with fixed eye search

/obj/structure/chair/bench/get_mechanics_examine(mob/user)
	. = ..()
	. += span_info("Some structures can be used as hiding places. Toggle the 'SNEAK' button on your HUD, then click the structure to hide in it. You can stop hiding by clicking the structure again, or by moving out of it.")

/obj/structure/chair/bench/church
	icon_state = "church_benchleft"

/obj/structure/chair/bench/church/mid
	icon_state = "church_benchmid"

/obj/structure/chair/bench/church/r
	icon_state = "church_benchright"

/obj/structure/chair/bench/Initialize()
	. = ..()
	var/static/list/loc_connections = list(COMSIG_ATOM_EXIT = PROC_REF(on_exit))
	AddElement(/datum/element/connect_loc, loc_connections)
	handle_layer()

/obj/structure/chair/bench/handle_layer()
	if(dir == NORTH)
		layer = ABOVE_MOB_LAYER
		plane = GAME_PLANE_UPPER
	else
		layer = OBJ_LAYER
		plane = GAME_PLANE

/obj/structure/chair/bench/attack_hand(mob/user)
	if(isliving(user))
		if(user.m_intent == MOVE_INTENT_SNEAK)
			hideinside(user)
			return

/obj/structure/chair/bench/proc/hideinside(mob/living/user)
	var/sneak_level = user.get_skill_level(/datum/skill/misc/sneaking) || 0
	var/sneaktime = max(10, 50 - (sneak_level * 10)) // Hard caps at 1 second at Expert and above.
	if(user.loc == src)
		unhide(user)
		return
	if(occupied)
		to_chat(user, span_warning("Someone is already hiding under [src]!"))
		return
	if(!do_after(user, sneaktime, src))
		return
	user.forceMove(src)
	occupied = TRUE
	hiddenguy = user
	to_chat(user, span_warning("I hide under [src]!"))

/obj/structure/chair/bench/proc/unhide(mob/living/user)
	var/turf/T = get_turf(src)
	if(!T) return
	user.forceMove(T)
	occupied = FALSE
	hiddenguy = null
	to_chat(user, span_warning("I come out from under [src]!"))

/obj/structure/chair/bench/relaymove(mob/user)
	if(user.loc == src)
		unhide(user)

/obj/structure/chair/bench/post_buckle_mob(mob/living/M)
	..()
	density = TRUE

/obj/structure/chair/bench/post_unbuckle_mob(mob/living/M)
	..()
	density = FALSE

/obj/structure/chair/bench/CanAStarPass(ID, travel_dir, caller)
	if(travel_dir == dir)
		return FALSE // don't even bother climbing over it
	return ..()

/obj/structure/chair/bench/CanPass(atom/movable/mover, turf/target)
	if(get_dir(mover,loc) == dir)
		return 0
	return !density

/obj/structure/chair/bench/proc/on_exit(datum/source, atom/movable/leaving, atom/new_location)
	SIGNAL_HANDLER
	if(istype(leaving, /obj/projectile))
		return
	if(get_dir(new_location, leaving.loc) == dir)
		leaving.Bump(src)
		return COMPONENT_ATOM_BLOCK_EXIT

/obj/structure/chair/bench/couch
	desc = "A comfortable-looking piece of furniture, padded with fabric!"
	icon_state = "redcouch"

/obj/structure/chair/bench/church/smallbench
	desc = "An unusually small, modest bench. Better than the floor."
	icon_state = "benchsmall"

/obj/structure/chair/bench/couch/r
	icon_state = "redcouch2"

/obj/structure/chair/bench/ultimacouch
	desc = "A particularly affluent piece of furniture, upholstered with brown-green fabric."
	icon_state = "ultimacouchleft"

/obj/structure/chair/bench/ultimacouch/r
	icon_state = "ultimacouchright"

/obj/structure/chair/bench/coucha
	desc = "A particularly affluent piece of furniture, upholstered with red fabric."
	icon_state = "couchaleft"

/obj/structure/chair/bench/coucha/r
	icon_state = "coucharight"

/obj/structure/chair/bench/couchablack
	desc = "A particularly affluent piece of furniture, upholstered with black fabric."
	icon_state = "couchablackaleft"

/obj/structure/chair/bench/couchablack/r
	icon_state = "couchablackaright"

/obj/structure/chair/bench/couchamagenta
	desc = "A particularly affluent piece of furniture, upholstered with magenta fabric."
	icon_state = "couchamagentaleft"

/obj/structure/chair/bench/couchamagenta/r
	icon_state = "couchamagentaright"

/obj/structure/chair/bench/couch/Initialize()
	. = ..()
	if(GLOB.lordprimary)
		lordcolor(GLOB.lordprimary,GLOB.lordsecondary)
	GLOB.lordcolor += src

/obj/structure/chair/bench/couch/Destroy()
	GLOB.lordcolor -= src
	return ..()

/obj/structure/chair/bench/couch/lordcolor(primary,secondary)
	if(!primary || !secondary)
		return
	var/mutable_appearance/M = mutable_appearance(icon, "[icon_state]_primary", -(layer+0.1))
	M.color = secondary //looks better
	add_overlay(M)

/obj/structure/chair/wood/rogue
	icon_state = "chair2"
	icon = 'icons/roguetown/misc/structure.dmi'
	item_chair = /obj/item/chair/rogue
	blade_dulling = DULLING_BASHCHOP
	destroy_sound = 'sound/combat/hits/onwood/destroyfurniture.ogg'
	attacked_sound = "woodimpact"

/obj/structure/chair/wood/rogue/Initialize()
	. = ..()
	var/static/list/loc_connections = list(COMSIG_ATOM_EXIT = PROC_REF(on_exit))
	AddElement(/datum/element/connect_loc, loc_connections)

/obj/structure/chair/wood/rogue/chair3
	icon_state = "chair3"
	icon = 'icons/roguetown/misc/structure.dmi'
	item_chair = /obj/item/chair/rogue
	blade_dulling = DULLING_BASHCHOP
	destroy_sound = 'sound/combat/hits/onwood/destroyfurniture.ogg'
	attacked_sound = "woodimpact"

/obj/structure/chair/wood/rogue/chair4
	icon_state = "chair4"
	icon = 'icons/roguetown/misc/structure.dmi'
	item_chair = /obj/item/chair/rogue
	blade_dulling = DULLING_BASHCHOP
	destroy_sound = 'sound/combat/hits/onwood/destroyfurniture.ogg'
	attacked_sound = "woodimpact"

/obj/structure/chair/wood/rogue/chair5
	icon_state = "chair5"
	icon = 'icons/roguetown/misc/structure.dmi'
	item_chair = /obj/item/chair/rogue
	blade_dulling = DULLING_BASHCHOP
	destroy_sound = 'sound/combat/hits/onwood/destroyfurniture.ogg'
	attacked_sound = "woodimpact"

/obj/structure/chair/wood/rogue/throne
	icon_state = "thronechair"
	desc = "Someone important must sit here."
	icon = 'icons/roguetown/misc/structure.dmi'
	blade_dulling = DULLING_BASHCHOP
	destroy_sound = 'sound/combat/hits/onwood/destroyfurniture.ogg'
	attacked_sound = "woodimpact"
	item_chair = null

/obj/item/chair/rogue
	name = "chair"
	icon = 'icons/roguetown/items/chairs.dmi'
	icon_state = "chair2"
	origin_type = /obj/structure/chair/wood/rogue
	blade_dulling = DULLING_BASHCHOP
	can_parry = TRUE
	force = 20
	force_wielded = 20
	throwforce = 25
	wdefense = 1
	possible_item_intents = list(/datum/intent/mace/strike/wood, /datum/intent/mace/smash/wood)
	gripped_intents = list(/datum/intent/mace/strike/wood, /datum/intent/mace/smash/wood)
	max_integrity = 50
	obj_flags = CAN_BE_HIT
	destroy_sound = 'sound/combat/hits/onwood/destroyfurniture.ogg'
	attacked_sound = "woodimpact"
	associated_skill = /datum/skill/combat/unarmed
	swingsound = BLUNTWOOSH_LARGE

/obj/item/chair/rogue/get_mechanics_examine(mob/user)
	. = ..()
	. += span_info("Click-dragging yourself onto a seat, while on the same tile, will 'buckle' you in; this represents your character sitting down. Sitting down gradually restores energy, and can be used to prop up people who'd normally be unable to stand.")
	. += span_info("Left-click on the seat to 'unbuckle' yourself; this represents your character getting up. You can alternatively get up by pressing the 'RESIST' hotkey.")
	. += span_info("This can be done to other people, as well. Click-dragging someone onto a seat will sit them down, and left-clicking the seat will 'unbuckle' them once more.")
	. += span_info("Restrained people are unable to 'unbuckle' themeselves, or otherwise get up from whatever they're tied to. Pressing the 'RESIST' hotkey will allow them to start struggling out of their restraints. The time needed to accomplish this scales with the character's Strength.")
	. += span_info("Some chairs and stools can be used as improvised weapons, by click-dragging them onto yourself. Activating a chair or stool in your hand will set it down in whatever direction you're facing.")

/obj/item/chair/rogue/getonmobprop(tag)
	. = ..()
	if(tag)
		switch(tag)
			if("gen") 
				return list("shrink" = 0.7,"sx" = -1,"sy" = 0,"nx" = 11,"ny" = 1,"wx" = 0,"wy" = 1,"ex" = 4,"ey" = 0,"northabove" = 0,"southabove" = 1,"eastabove" = 1,"westabove" = 0,"nturn" = 15,"sturn" = 0,"wturn" = 0,"eturn" = 39,"nflip" = 8,"sflip" = 0,"wflip" = 0,"eflip" = 8)
			if("wielded") 
				return list("shrink" = 0.7,"sx" = -9,"sy" = 4,"nx" = -7,"ny" = 0,"wx" = -7,"wy" = 2,"ex" = 8,"ey" = 5,"northabove" = 0,"southabove" = 1,"eastabove" = 1,"westabove" = 0,"nturn" = -42,"sturn" = 190,"wturn" = -170,"eturn" = -10,"nflip" = -8,"sflip" = 1,"wflip" = 1,"eflip" = 0)

/obj/structure/chair/wood/rogue/CanPass(atom/movable/mover, turf/target)
	if(isliving(mover))
		var/mob/living/M = mover
		if((M.mobility_flags & MOBILITY_STAND))
			if(isturf(loc))
				var/movefrom = get_dir(M.loc, loc)
				if(movefrom == dir && item_chair)
					playsound(loc, 'sound/foley/chairfall.ogg', 100, FALSE)
					var/obj/item/I = new item_chair(loc)
					item_chair = null
					I.dir = dir
					qdel(src)
					return FALSE
	return ..()


/obj/structure/chair/wood/rogue/onkick(mob/user)
	if(!user)
		return
	if(!item_chair)
		return
	if(isturf(loc))
		playsound(loc, 'sound/foley/chairfall.ogg', 100, FALSE)
		var/obj/item/I = new item_chair(loc)
		item_chair = null
		I.dir = dir
		qdel(src)
		return FALSE

/obj/structure/chair/wood/rogue/proc/on_exit(datum/source, atom/movable/leaving, atom/new_location)
    SIGNAL_HANDLER
    if(!isliving(leaving))
        return
    var/mob/living/M = leaving
    if(!(M.mobility_flags & MOBILITY_STAND))
        return
    if(get_dir(leaving.loc, new_location) == REVERSE_DIR(dir))
        if(!item_chair)
            return
        playsound(loc, 'sound/foley/chairfall.ogg', 100, FALSE)
        var/obj/item/I = new item_chair(loc)
        item_chair = null
        I.dir = dir
        qdel(src)
        return COMPONENT_ATOM_BLOCK_EXIT

/obj/structure/chair/wood/rogue/take_damage(damage_amount, damage_type = BRUTE, damage_flag = 0, sound_effect = 1)
	if(damage_amount > 5 && item_chair != null)
		playsound(loc, 'sound/foley/chairfall.ogg', 100, FALSE)
		var/obj/item/I = new item_chair(loc)
		item_chair = null
		I.dir = dir
		qdel(src)
		return FALSE
	return ..()

/obj/structure/chair/wood/rogue/fancy
	icon_state = "chair1"
	item_chair = /obj/item/chair/rogue/fancy

/obj/item/chair/rogue/fancy
	icon_state = "chair1"
	origin_type = /obj/structure/chair/wood/rogue/fancy

/obj/structure/chair/wood/rogue/attack_right(mob/user)
	var/datum/component/simple_rotation/rotcomp = GetComponent(/datum/component/simple_rotation)
	if(rotcomp)
		rotcomp.HandRot(rotcomp,user,ROTATION_CLOCKWISE)

/obj/structure/chair/wood/rogue
//	pixel_y = 5

/obj/structure/chair/wood/rogue/post_buckle_mob(mob/living/M)
	..()
	density = TRUE
//	M.set_mob_offsets("bed_buckle", _x = 0, _y = 5)

/obj/structure/chair/wood/rogue/post_unbuckle_mob(mob/living/M)
	..()
	density = FALSE
//	M.reset_offsets("bed_buckle")


/obj/structure/chair/stool/rogue
	name = "stool"
	desc = "Three stubby legs nailed to the underside of a small round seat. Stable, if simple."
	icon_state = "barstool"
	icon = 'icons/roguetown/misc/structure.dmi'
	item_chair = /obj/item/chair/stool/bar/rogue
	max_integrity = 100
	blade_dulling = DULLING_BASHCHOP
	destroy_sound = 'sound/combat/hits/onwood/destroyfurniture.ogg'
	attacked_sound = "woodimpact"

/obj/item/chair/stool/bar/rogue
	name = "stool"
	desc = "Three stubby legs nailed to the underside of a small round seat. Stable, if simple."
	icon_state = "baritem"
	icon = 'icons/roguetown/misc/structure.dmi'
	origin_type = /obj/structure/chair/stool/rogue
	blade_dulling = DULLING_BASHCHOP
	can_parry = TRUE
	force = 15
	force_wielded = 15
	throwforce = 20
	wdefense = 1
	possible_item_intents = list(/datum/intent/mace/strike/wood, /datum/intent/mace/smash/wood)
	gripped_intents = list(/datum/intent/mace/strike/wood, /datum/intent/mace/smash/wood)
	max_integrity = 50
	obj_flags = CAN_BE_HIT
	destroy_sound = 'sound/combat/hits/onwood/destroyfurniture.ogg'
	attacked_sound = "woodimpact"
	associated_skill = /datum/skill/combat/unarmed
	swingsound = BLUNTWOOSH_LARGE

/obj/item/chair/stool/bar/rogue/get_mechanics_examine(mob/user)
	. = ..()
	. += span_info("Some chairs and stools can be used as improvised weapons, by click-dragging them onto yourself. Activating a chair or stool in your hand will set it down in whatever direction you're facing.")

/obj/item/chair/stool/bar/rogue/getonmobprop(tag)
	. = ..()
	if(tag)
		switch(tag)
			if("gen") 
				return list("shrink" = 0.7,"sx" = -1,"sy" = 0,"nx" = 11,"ny" = 1,"wx" = 0,"wy" = 1,"ex" = 4,"ey" = 0,"northabove" = 0,"southabove" = 1,"eastabove" = 1,"westabove" = 0,"nturn" = 15,"sturn" = 0,"wturn" = 0,"eturn" = 39,"nflip" = 8,"sflip" = 0,"wflip" = 0,"eflip" = 8)
			if("wielded") 
				return list("shrink" = 0.7, "sx" = -12, "sy" = -8, "nx" = 13, "ny" = -7, "wx" = -10, "wy" = -5, "ex" = 7, "ey" = -6, "northabove" = 0, "southabove" = 1, "eastabove" = 1, "westabove" = 0, "nturn" = -13, "sturn" = 110, "wturn" = -60, "eturn" = -30, "nflip" = 1, "sflip" = 1, "wflip" = 8, "eflip" = 1)

/obj/structure/bed/rogue
	icon_state = "bed"
	icon = 'icons/roguetown/misc/beds.dmi'
	anchored = TRUE
	can_buckle = TRUE
	buckle_lying = 90
	sleepy = 3
	debris = list(/obj/item/grown/log/tree/small = 1)
	hidingspot = TRUE
	var/mob/living/hiddenguy = null // So we can find them with fixed eye search

/obj/structure/bed/rogue/get_mechanics_examine(mob/user)
	. = ..()
	. += span_info("Click-dragging yourself onto a bed, while on the same tile, will 'buckle' you in; this represents your character laying down. Laying down gradually restores energy, and allows your character to begin sleeping.")
	. += span_info("Once laying down on a bed, you can close your eyes by clicking the small arrows that border the top-and-bottom of your HUD's eye. If your eyes are closed and you're laying down, you'll begin to drift off into sleep.")
	. += span_info("Most people can't sleep while wearing helmets and chestpieces. Left-click the items-in-question in your inventory to take them off. You can either press the 'Z' key to drop them, or left-click a table or opened closet to set them aside.")
	. += span_info("If you aren't interrupted, you'll fall asleep after a bit. Sleeping clears the daily 'Tired' debuff and allows you to further learn more skills. The higher your Intelligence is, the more skills - and skill points - you'll earn with each sleep.")
	. += span_info("Sleeping will gradually heal most wounds and damages, over time. This can be further enhanced by sleeping next to a lit campfire or fireplace. To begin waking back up, click the arrows that border the HUD's eye once again. If you can see the eye, that means you'll wake up soon.")
	. += span_info("Once awake, hit the 'RESIST' hotkey or left-click the bed to 'unbuckle' yourself. Once unbuckled, pressing the 'V' key will allow you to fully rise up.")
	. += span_info("Note that you can still sleep anywhere you wish, even without a bed, by simply laying down and closing your eyes. While this can work in a pinch to stave off tiredness or bolster your characters to survive a critical wound, it's much less ideal.")
	. += span_info("Some structures can be used as hiding places. Toggle the 'SNEAK' button on your HUD, then click the structure to hide in it. You can stop hiding by clicking the structure again, or by moving out of it.")

/obj/structure/bed/rogue/OnCrafted(dirin)
	dirin = turn(dirin, 180)
	. = ..(dirin)
	update_icon()

/obj/structure/bed/rogue/attack_hand(mob/living/user)
	if(user.m_intent == MOVE_INTENT_SNEAK)
		hideinside(user)
		return
	return ..()

/obj/structure/bed/rogue/proc/hideinside(mob/living/user)
	var/sneak_level = user.get_skill_level(/datum/skill/misc/sneaking) || 0
	var/sneaktime = max(10, 50 - (sneak_level * 10)) // Hard caps at 1 second at Expert and above.
	if(user.loc == src)
		unhide(user)
		return
	if(occupied)
		to_chat(user, span_warning("Someone is already hiding under [src]!"))
		return
	if(!do_after(user, sneaktime, src))
		return
	user.forceMove(src)
	occupied = TRUE
	hiddenguy = user
	to_chat(user, span_warning("I hide under [src]!"))

/obj/structure/bed/rogue/proc/unhide(mob/living/user)
	var/turf/T = get_turf(src)
	if(!T) return
	user.forceMove(T)
	occupied = FALSE
	hiddenguy = null
	to_chat(user, span_warning("I come out from under [src]!"))

/obj/structure/bed/rogue/relaymove(mob/user)
	if(user.loc == src)
		unhide(user)

/obj/structure/bed/rogue/attack_right(mob/user)
	var/datum/component/simple_rotation/rotcomp = GetComponent(/datum/component/simple_rotation)
	if(rotcomp)
		rotcomp.HandRot(rotcomp,user,ROTATION_CLOCKWISE)

/obj/structure/bed/rogue/shit
	name = "straw bed"
	desc = "A rough bed of straw. It's scratchy, and probably hides lots of bugs, but at least it's dry and warm."
	icon_state = "shitbed"
	sleepy = 1.5

/obj/structure/bed/rogue/post_buckle_mob(mob/living/M)
	..()
	M.set_mob_offsets("bed_buckle", _x = 0, _y = 5)

/obj/structure/bed/rogue/post_unbuckle_mob(mob/living/M)
	..()
	M.reset_offsets("bed_buckle")

/obj/structure/bed/rogue/bedroll
	name = "bedroll"
	desc = "So you can sleep on the ground in relative peace."
	icon_state = "bedroll"
	attacked_sound = 'sound/foley/cloth_rip.ogg'
	break_sound = 'sound/foley/cloth_rip.ogg'
	sleepy = 2

/obj/structure/bed/rogue/bedroll/attack_hand(mob/user, params)
	..()
	user.visible_message(span_notice("[user] begins rolling up \the [src]."))
	if(do_after(user, 2 SECONDS, TRUE, src))
		var/obj/item/bedroll/new_bedroll = new /obj/item/bedroll(get_turf(src))
		new_bedroll.color = src.color
		qdel(src)

/obj/item/bedroll
	name = "rolled bedroll"
	icon = 'icons/roguetown/items/misc.dmi'
	icon_state = "bedroll_r"
	w_class = WEIGHT_CLASS_NORMAL
	slot_flags = ITEM_SLOT_HIP | ITEM_SLOT_BACK
	grid_width = 32
	grid_height = 64

/obj/item/bedroll/get_mechanics_examine(mob/user)
	. = ..()
	. += span_info("Activate the bedroll in your hand to set it down, and left-click the placed-down bedroll to pick it back up.")

/obj/item/bedroll/attack_self(mob/user, params)
	..()
	var/turf/T = get_turf(loc)
	if(!isfloorturf(T))
		to_chat(user, span_warning("I need ground to plant this on!"))
		return
	for(var/obj/A in T)
		if(istype(A, /obj/structure))
			to_chat(user, span_warning("I need some free space to deploy a [src] here!"))
			return
		if(A.density && !(A.flags_1 & ON_BORDER_1))
			to_chat(user, span_warning("There is already something here!</span>"))
			return
	user.visible_message(span_notice("[user] begins placing \the [src] down on the ground."))
	if(do_after(user, 2 SECONDS, TRUE, src))
		var/obj/structure/bed/rogue/bedroll/new_bedroll = new /obj/structure/bed/rogue/bedroll(get_turf(src))
		new_bedroll.color = src.color
		qdel(src)

/obj/item/bedroll/getonmobprop(tag)
	. = ..()
	if(tag)
		switch(tag)
			if("gen")
				return list("shrink" = 0.5,"sx" = -1,"sy" = -1,"nx" = 7,"ny" = -1,"wx" = 0,"wy" = -1,"ex" = 4,"ey" = -2,"northabove" = 0,"southabove" = 1,"eastabove" = 1,"westabove" = 0,"nturn" = 15,"sturn" = 0,"wturn" = 0,"eturn" = 39,"nflip" = 8,"sflip" = 0,"wflip" = 0,"eflip" = 8)
			if("onbelt")
				return list("shrink" = 0.3,"sx" = -2,"sy" = -5,"nx" = 4,"ny" = -5,"wx" = 0,"wy" = -5,"ex" = 2,"ey" = -5,"nturn" = 0,"sturn" = 0,"wturn" = 0,"eturn" = 0,"nflip" = 0,"sflip" = 0,"wflip" = 0,"eflip" = 0,"northabove" = 0,"southabove" = 1,"eastabove" = 1,"westabove" = 0)

/obj/structure/bed/rogue/inn
	icon_state = "inn_bed"
	icon = 'icons/roguetown/misc/beds.dmi'
	anchored = TRUE
	can_buckle = TRUE
	buckle_lying = 90
	sleepy = 3
	debris = list(/obj/item/grown/log/tree/small = 1)

/obj/structure/bed/rogue/inn/wooldouble
	icon_state = "double_wool"
	icon = 'icons/roguetown/misc/beds.dmi'
	anchored = TRUE
	can_buckle = TRUE
	buckle_lying = 90
	pixel_y = 0
	sleepy = 3
	debris = list(/obj/item/grown/log/tree/small = 2)

/obj/structure/bed/rogue/inn/double
	icon_state = "double"
	icon = 'icons/roguetown/misc/beds.dmi'
	anchored = TRUE
	can_buckle = TRUE
	buckle_lying = 90
	pixel_y = 0
	sleepy = 3
	debris = list(/obj/item/grown/log/tree/small = 2)
/*            ///////WIP  This will essentially allow for multiple mobs to buckle, just needs to change mousedrop function
/obj/structure/bed/rogue/inn/double
	var/list/buckled_mobs = list()

/obj/structure/bed/rogue/inn/double/post_buckle_mob(mob/living/M)
	. = ..()
	if(!buckled_mobs)
		buckled_mobs = list()
	buckled_mobs += M
	M.set_mob_offsets("bed_buckle", _x = buckled_mobs.len * 10, _y = 5)

/obj/structure/bed/rogue/inn/double/post_unbuckle_mob(mob/living/M)
	. = ..()
	if(M in buckled_mobs)
		buckled_mobs -= M
	M.reset_offsets("bed_buckle")

	var/x_offset = 0
	for(var/mob/living/buckled_mob in buckled_mobs)
		buckled_mob.set_mob_offsets("bed_buckle", _x = x_offset, _y = 5)
		x_offset += 10
*/
/obj/structure/bed/rogue/inn/hay
	icon_state = "haybed"
	icon = 'icons/roguetown/misc/beds.dmi'
	anchored = TRUE
	can_buckle = TRUE
	buckle_lying = 90
	sleepy = 2
	debris = list(/obj/item/grown/log/tree/small = 1)

/obj/structure/bed/rogue/inn/wool
	icon_state = "woolbed"
	icon = 'icons/roguetown/misc/beds.dmi'
	anchored = TRUE
	can_buckle = TRUE
	buckle_lying = 90
	sleepy = 3
	debris = list(/obj/item/grown/log/tree/small = 1)

/obj/structure/bed/rogue/inn/pileofshit
	icon_state = "shitbed2"
	icon = 'icons/roguetown/misc/beds.dmi'
	anchored = TRUE
	can_buckle = TRUE
	buckle_lying = 90
	sleepy = 1
