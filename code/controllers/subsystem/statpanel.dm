/// Listed-turf lag guard: hard-cap total entries shown.
#define STATBROWSER_TURF_HARD_CAP 250
#define STATBROWSER_TURF_ICON_CAP 25

SUBSYSTEM_DEF(statpanels)
	name = "Stat Panels"
	wait = 4
	priority = FIRE_PRIORITY_STATPANEL
	runlevels = RUNLEVELS_DEFAULT | RUNLEVEL_LOBBY
	flags = SS_NO_INIT
	var/list/currentrun = list()
	var/list/global_data
	var/list/mc_data

	///how many subsystem fires between most tab updates
	var/default_wait = 10
	///how many subsystem fires between updates of the status tab
	var/status_wait = 2
	///how many subsystem fires between updates of the MC tab
	var/mc_wait = 5
	///how many full runs this subsystem has completed. used for variable rate refreshes.
	var/num_fires = 0

/datum/controller/subsystem/statpanels/fire(resumed = FALSE)
	if (!resumed)
		num_fires++
		var/datum/map_config/cached = SSmapping.next_map_config

		if(isnull(SSmapping.config))
			global_data = list("Loading")
		else
			global_data = list("Map: [SSmapping.config.map_name]")

		// if(SSmapping.config?.mapping_url)
		// 	global_data += list(list("same_line", " | (View in Browser)", "action=openWebMap"))

		if(cached)
			global_data += "Next Map: [cached.map_name]"


		var/true_round_time = "[ROUND_TIME()]"
		if(SSticker.HasRoundStarted())
			true_round_time = time2text(world.time - SSticker.round_start_time, "hh:mm:ss", 0)
		global_data += list(
			"Round ID: [GLOB.rogue_round_id ? GLOB.rogue_round_id : "NULL"]",
			"Server Time: [time2text(world.timeofday, "YYYY-MM-DD hh:mm:ss", world.timezone)]",
			"Round Time: [true_round_time] | TrueTime: [worldtime2text()] ([world.time])",
			list("load", load_class(world.map_cpu), "Time Dilation: [round(SStime_track.time_dilation_current,1)]% AVG:([round(SStime_track.time_dilation_avg_fast,1)]%, [round(SStime_track.time_dilation_avg,1)]%, [round(SStime_track.time_dilation_avg_slow,1)]%) | Map Tick: ", "[round(world.map_cpu, 1)]%"),
		)

		if(SSgamemode.roundvoteend)
			var/time_left = SSgamemode.round_ends_at - world.time
			global_data += "Round End: [DisplayTimeText(time_left, 1)]"

		if(SSticker.ready_for_reboot)
			global_data += "Reboot: DELAYED"

		src.currentrun = GLOB.clients.Copy()
		mc_data = null

	var/list/currentrun = src.currentrun
	while(length(currentrun))
		var/client/target = currentrun[length(currentrun)]
		currentrun.len--

		if(!target.stat_panel.is_ready())
			continue

		if(target.stat_tab == "Round Info" && num_fires % status_wait == 0)
			set_status_tab(target)

		if(isliving(target.mob))
			if(!target.statbrowser_stats_shown)
				target.stat_panel.send_message("add_stats_tab")
				target.statbrowser_stats_shown = TRUE
			if(target.stat_tab == "Stats" && num_fires % status_wait == 0)
				set_stats_tab(target)
		else if(target.statbrowser_stats_shown)
			target.stat_panel.send_message("remove_stats_tab")
			target.statbrowser_stats_shown = FALSE

		if((target.mob?.listed_turf || target.listedturf_sig) && (target.listedturf_dirty || (num_fires % default_wait == 0)))
			target.listedturf_dirty = FALSE
			target.update_listed_turf()

		if(!target.holder)
			target.stat_panel.send_message("remove_admin_tabs")
		else
			if(!("MC" in target.panel_tabs) || !("Tickets" in target.panel_tabs))
				target.stat_panel.send_message("add_admin_tabs", target.holder.href_token)

			if(target.stat_tab == "MC" && ((num_fires % mc_wait == 0)))
				set_MC_tab(target)

			if(target.stat_tab == "Tickets" && num_fires % default_wait == 0)
				set_tickets_tab(target)

			if(!length(GLOB.sdql2_queries) && ("SDQL2" in target.panel_tabs))
				target.stat_panel.send_message("remove_sdql2")

			else if(length(GLOB.sdql2_queries) && (target.stat_tab == "SDQL2" || !("SDQL2" in target.panel_tabs)) && num_fires % default_wait == 0)
				set_SDQL2_tab(target)

		if(target.mob)
			var/mob/target_mob = target.mob

			// Handle the action panels of the stat panel

			var/update_actions = FALSE
			// // We're on a spell tab, update the tab so we can see cooldowns progressing and such
			// if(target.stat_tab in target.spell_tabs)
			// 	update_actions = TRUE
			// // We're not on a spell tab per se, but we have cooldown actions, and we've yet to
			// // set up our spell tabs at all
			// if(!length(target.spell_tabs) && locate(/datum/action/cooldown) in target_mob.actions)
			// 	update_actions = TRUE

			if(update_actions && num_fires % default_wait == 0)
				set_action_tabs(target, target_mob)

		if(MC_TICK_CHECK)
			return

/*
 * send_message for the stat panel can be sent 1 of 4 things:
 * 1- A string entry, to show up as plain text.
 * 2- An empty string (""), which will translate to a new line, to for a break between lines.
 * 3- a list, in which the first entry is plain text, the second entry is highlighted text, and the third entry is a link
 * that clicking the second entry will take you to.
 * 4- a list with "same_line" as the first entry, which will automatically put it on the line above it,
 * with the second/third entry matching #3 (text & url), allowing you to have 2 clickable links on one line.
 */
/datum/controller/subsystem/statpanels/proc/set_status_tab(client/target)
	if(!global_data)//statbrowser hasnt fired yet and we were called from immediate_send_stat_data()
		return
	target.stat_panel.send_message("update_stat", list(
		"global_data" = global_data,
		"ping_str" = "Ping: [round(target.lastping, 1)]ms (Average: [round(target.avgping, 1)]ms)",
		"other_str" = target.mob?.get_status_tab_items(),
	))

/datum/controller/subsystem/statpanels/proc/set_stats_tab(client/target)
	var/mob/M = target.mob
	if(!M)
		return
	target.stat_panel.send_message("update_stats", M.get_stats_tab_items())

/datum/controller/subsystem/statpanels/proc/load_class(percent)
	if(percent >= 80)
		return "high"
	if(percent >= 50)
		return "mid"
	return "low"

/datum/controller/subsystem/statpanels/proc/set_MC_tab(client/target)
	var/turf/eye_turf = get_turf(target.eye)
	var/coord_entry = COORD(eye_turf)
	if(!mc_data)
		generate_mc_data()
	target.stat_panel.send_message("update_mc", list("mc_data" = mc_data, "coord_entry" = coord_entry))

/datum/controller/subsystem/statpanels/proc/set_tickets_tab(client/target)
	var/list/ahelp_tickets = GLOB.ahelp_tickets.stat_entry()
	target.stat_panel.send_message("update_tickets", ahelp_tickets)

/datum/controller/subsystem/statpanels/proc/set_SDQL2_tab(client/target)
	var/list/sdql2A = list()
	sdql2A[++sdql2A.len] = list("", "Access Global SDQL2 List", REF(GLOB.sdql2_vv_statobj))
	var/list/sdql2B = list()
	for(var/datum/SDQL2_query/query as anything in GLOB.sdql2_queries)
		sdql2B = query.generate_stat()

	sdql2A += sdql2B
	target.stat_panel.send_message("update_sdql2", sdql2A)

/// Set up the various action tabs.
/datum/controller/subsystem/statpanels/proc/set_action_tabs(client/target, mob/target_mob)
	// var/list/actions = target_mob.get_actions_for_statpanel()
	// target.spell_tabs.Cut()

	// for(var/action_data in actions)
	// 	target.spell_tabs |= action_data[1]

	// target.stat_panel.send_message("update_spells", list(spell_tabs = target.spell_tabs, actions = actions))

/datum/controller/subsystem/statpanels/proc/generate_mc_data()
	mc_data = list(
		list("", "CPU:", world.cpu),
		list("", "Instances:", "[num2text(world.contents.len, 10)]"),
		list("", "World Time:", "[world.time]"),
		list("", "Globals:", GLOB.stat_entry(), text_ref(GLOB)),
		list("", "[config]:", config.stat_entry(), text_ref(config)),
		list("", "Master Controller:", Master.stat_entry(), text_ref(Master)),
		list("", "Failsafe Controller:", Failsafe.stat_entry(), text_ref(Failsafe)),
		list("", "", "")
	)

	for(var/datum/controller/subsystem/sub_system as anything in Master.subsystems)
		mc_data[++mc_data.len] = list("\[[sub_system.state_letter()]]", sub_system.name, sub_system.stat_entry(), text_ref(sub_system))

///immediately update the active statpanel tab of the target client
/datum/controller/subsystem/statpanels/proc/immediate_send_stat_data(client/target)
	if(!target.stat_panel.is_ready())
		return FALSE

	if(target.stat_tab == "Round Info")
		set_status_tab(target)
		return TRUE

	if(target.stat_tab == "Stats")
		set_stats_tab(target)
		return TRUE

	if(target.mob?.listed_turf && target.stat_tab == "[target.mob.listed_turf]")
		target.send_listed_turf()
		return TRUE

	var/mob/target_mob = target.mob

	// Handle actions

	var/update_actions = FALSE
	if(update_actions)
		set_action_tabs(target, target_mob)
		return TRUE

	if(!target.holder)
		return FALSE

	if(target.stat_tab == "MC")
		set_MC_tab(target)
		return TRUE

	if(target.stat_tab == "Tickets")
		set_tickets_tab(target)
		return TRUE

	if(!length(GLOB.sdql2_queries) && ("SDQL2" in target.panel_tabs))
		target.stat_panel.send_message("remove_sdql2")

	else if(length(GLOB.sdql2_queries) && target.stat_tab == "SDQL2")
		set_SDQL2_tab(target)

/// Stat panel window declaration
/client/var/datum/tgui_window/stat/stat_panel

/datum/tgui_window/stat/initialize(strict_mode, fancy, assets, inline_html, inline_js, inline_css)
	. = ..()
	send_message("build_topbar") // This is the best way of doing it... don't @ me

/client/proc/open_listed_turf(turf/T)
	if(!mob || !T || !mob.TurfAdjacent(T))
		return
	if(mob.listed_turf)
		LAZYREMOVE(mob.listed_turf.panel_listeners, src)
	mob.listed_turf = T
	LAZYADD(T.panel_listeners, src)
	listedturf_sig = null
	stat_panel.send_message("create_listedturf", "[T]")
	send_listed_turf()

/client/proc/listedturf_signature(turf/T)
	return "[REF(T)]|[length(T.contents)]|[mob?.see_invisible]"

/client/proc/update_listed_turf()
	var/turf/T = mob?.listed_turf
	if(!T || !mob.TurfAdjacent(T))
		if(listedturf_sig != null)
			if(T)
				LAZYREMOVE(T.panel_listeners, src)
			mob?.listed_turf = null
			listedturf_sig = null
			clear_listedturf_appearances()
			stat_panel.send_message("remove_listedturf")
		return
	if(stat_tab == "[T]" && listedturf_sig != listedturf_signature(T))
		send_listed_turf()

/client/proc/send_listed_turf()
	var/turf/T = mob?.listed_turf
	if(!T)
		return
	clear_listedturf_appearances()
	listedturf_sig = listedturf_signature(T)
	var/list/overrides = list()
	for(var/image/override_image in images)
		if(override_image.override && override_image.loc?.loc == T)
			overrides += override_image.loc
	var/list/data = list(list(statpanel_strip_article("[T]"), REF(T), listedturf_icon(T)))
	var/shown = 0
	for(var/atom/thing in T)
		if(!ismob(thing) && !thing.mouse_opacity)
			continue
		if(thing.invisibility > mob.see_invisible)
			continue
		if(length(overrides) && !ismob(thing) && (thing in overrides))
			continue
		if(shown >= STATBROWSER_TURF_HARD_CAP)
			data += list(list("...more contents not shown", null, null))
			break
		var/thing_icon = (shown < STATBROWSER_TURF_ICON_CAP) ? listedturf_icon(thing) : null
		data += list(list(statpanel_strip_article("[thing]"), REF(thing), thing_icon))
		shown++
	stat_panel.send_message("update_listedturf", data)

/client/proc/listedturf_icon(atom/thing)
	if(!thing.icon)
		return null
	if(ismob(thing) || length(thing.overlays) > 2 || !isfile(thing.icon))
		var/atom/movable/screen/container = mob?.send_appearance(copy_appearance_filter_overlays(thing.appearance), 0)
		if(container)
			LAZYADD(listedturf_appearances, container)
			return "\ref[container]"
	return "\ref[thing.icon]?state=[thing.icon_state]&dir=[thing.dir]"

/client/proc/clear_listedturf_appearances()
	if(!LAZYLEN(listedturf_appearances))
		return
	var/datum/hud/our_hud = mob?.hud_used
	if(our_hud?.vis_holder)
		for(var/atom/movable/screen/container as anything in listedturf_appearances)
			our_hud.vis_holder.vis_contents -= container
	listedturf_appearances = null

/proc/statpanel_strip_article(name)
	if(findtext(name, "the ") == 1)
		return copytext(name, 5)
	return name

/atom/Topic(href, list/href_list)
	. = ..()
	if(!href_list["statpanel_item_click"])
		return
	var/mob/user = usr
	if(!user || !user.client)
		return
	var/list/paramslist = list()
	switch(href_list["statpanel_item_click"])
		if("left")
			paramslist[LEFT_CLICK] = "1"
		if("right")
			paramslist[RIGHT_CLICK] = "1"
		if("middle")
			paramslist[MIDDLE_CLICK] = "1"
		else
			return
	if(href_list["statpanel_item_shiftclick"])
		paramslist[SHIFT_CLICKED] = "1"
	if(href_list["statpanel_item_ctrlclick"])
		paramslist[CTRL_CLICKED] = "1"
	if(href_list["statpanel_item_altclick"])
		paramslist[ALT_CLICKED] = "1"
	user.client.Click(src, loc, null, list2params(paramslist))

#undef STATBROWSER_TURF_HARD_CAP
#undef STATBROWSER_TURF_ICON_CAP
