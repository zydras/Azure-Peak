#define DRAGONHIDE_FILTER "dragonhide_glow"

/datum/status_effect/buff/dragonhide
	id = "dragonscaled"
	alert_type = /atom/movable/screen/alert/status_effect/buff/dragonhide
	duration = -1
	effectedstats = list(STATKEY_CON = 1)
	var/outline_colour = "#c23d09"

/atom/movable/screen/alert/status_effect/buff/dragonhide
	name = "Dragonhide"
	desc = "Draconic scales shield me from the worst of the flames."

/datum/status_effect/buff/dragonhide/on_apply()
	. = ..()
	ADD_TRAIT(owner, TRAIT_FIRE_RESIST, TRAIT_GENERIC)
	var/filter = owner.get_filter(DRAGONHIDE_FILTER)
	if (!filter)
		owner.add_filter(DRAGONHIDE_FILTER, 2, list("type" = "outline", "color" = outline_colour, "alpha" = 60, "size" = 1))

/datum/status_effect/buff/dragonhide/on_remove()
	. = ..()
	REMOVE_TRAIT(owner, TRAIT_FIRE_RESIST, TRAIT_GENERIC)
	owner.remove_filter(DRAGONHIDE_FILTER)

#undef DRAGONHIDE_FILTER

/datum/status_effect/buff/dragonhide/astrata
	id = "asbestine"
	alert_type = /atom/movable/screen/alert/status_effect/buff/dragonhide/astrata
	duration = 2 MINUTES
	outline_colour = "#9B7439"

/atom/movable/screen/alert/status_effect/buff/dragonhide/astrata
	name = "Asbestine Cloak"
	desc = "Enveloped in Her radiance flames bear no hold over me!"
