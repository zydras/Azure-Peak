/datum/round_event_control/worldsiege
	name = "Skeleton Siege"
	typepath = /datum/round_event/worldsiege
	weight = 10
	max_occurrences = 1
	min_players = 4
	earliest_start = 35 MINUTES
	tags = list(TAG_HAUNTED, TAG_COMBAT, TAG_RAID)
	track = EVENT_TRACK_RAIDS
	var/last_siege
	var/raid_text = "The skeleton horde approaches."

/datum/round_event_control/worldsiege/canSpawnEvent(players_amt, gamemode, fake_check)
	if(earliest_start >= world.time-SSticker.round_start_time)
		return FALSE
	if(players_amt < min_players)
		return FALSE
	. = ..()

/datum/round_event_control/worldsiege/preRunEvent()
	. = ..()
	if(. == EVENT_READY)
		priority_announce(raid_text, "", 'sound/misc/evilevent.ogg')
	last_siege = world.time

/datum/round_event/worldsiege
	announceWhen	= 1

/datum/round_event/worldsiege/setup()
	return TRUE

/datum/round_event/worldsiege/start()
	SSmapping.add_world_trait(/datum/world_trait/skeleton_siege, rand(10 MINUTES, 15 MINUTES)) //These guys literally will fold over to just about anything.
	//Lasts roughly up to an ingame day or slightly longer on rare occasions, these guys are genuinely abysmal, dust on death and have terrible stats and... moderately okay skills I guess.
	for(var/mob/dead/observer/O in GLOB.player_list)
		addtimer(CALLBACK(O, TYPE_PROC_REF(/mob/dead/observer, horde_respawn)), 1)
	return
