/obj/item/parcel
	name = "parcel wrapping paper"
	desc = "A sturdy piece of paper used to wrap items for secure delivery. The final size of the parcel depends on the size of its contents."
	icon = 'modular/Neu_Food/icons/cookware/ration.dmi'
	icon_state = "ration_wrapper"
	w_class = WEIGHT_CLASS_TINY
	grid_height = 32
	grid_width = 32
	dropshrink = 0.6
	/// Items contained in this parcel. Courier quests pack a single item, Recovery quests pack
	/// 4-6. Released to the turf when an authorized recipient unwraps the seal.
	var/list/obj/item/contained_items = list()
	var/list/allowed_jobs = list()
	var/delivery_area_type
	var/datum/proximity_monitor/proximity_monitor

/obj/item/parcel/Initialize(mapload)
	. = ..()
	var/datum/component/quest_object/courier_quest = GetComponent(/datum/component/quest_object)
	if(courier_quest)
		var/datum/quest/quest = courier_quest.quest_ref?.resolve()
		if(quest && quest.quest_type == QUEST_COURIER && quest.target_delivery_location)
			delivery_area_type = quest.target_delivery_location
			allowed_jobs = get_area_jobs(delivery_area_type)
			RegisterSignal(courier_quest, COMSIG_PARENT_QDELETING, PROC_REF(on_quest_component_deleted))

	invisibility = INVISIBILITY_OBSERVER
	proximity_monitor = new(src, 5)

/obj/item/parcel/HasProximity(mob/nearby)
	if(!istype(nearby))
		return

	var/datum/component/quest_object/quest_component = GetComponent(/datum/component/quest_object)
	if(!istype(quest_component))
		return

	var/datum/quest/quest = quest_component.quest_ref?.resolve()
	if(!istype(quest))
		return

	if(get_dist(get_turf(src), get_turf(quest.quest_scroll_ref?.resolve())) > 5)
		return

	var/image/I = image(icon = 'icons/effects/effects.dmi', loc = get_turf(src), icon_state = "hidden", layer = 18)
	I.layer = 18
	I.plane = 18
	if(!I)
		return
	I.mouse_opacity = MOUSE_OPACITY_TRANSPARENT
	flick_overlay_view(I, 5 SECONDS)
	invisibility = initial(invisibility)
	QDEL_NULL(proximity_monitor)

/obj/item/parcel/proc/get_area_jobs(area_type)
	var/static/list/area_jobs = list(
		/area/rogue/indoors/town/tavern = list("Innkeeper", "Tapster", "Cook"),
		/area/rogue/indoors/town/bath = list("Bathhouse Attendant", "Bathmaster"),
		/area/rogue/indoors/town/church = list("Bishop", "Acolyte", "Templar", "Sexton", "Martyr"),
		/area/rogue/indoors/town/dwarfin = list("Guildmaster", "Guildsman"),
		/area/rogue/indoors/town/shop = list("Merchant", "Shophand"),
		/area/rogue/indoors/town/manor = list("Councillor", "Seneschal", "Servant", "Hand", "Knight", "Marshal", "Steward", "Clerk", "Grand Duke"),
		/area/rogue/indoors/town/magician = list("Court Magician", "Magicians Associate", "Archivist"),
		/area/rogue/indoors/town/physician = list("Physician", "Apothecary"),
		/area/rogue/indoors/town = list("Guild Handler")
	)
	return area_jobs[area_type] || list("Town Crier", "Steward", "Merchant")

/obj/item/parcel/proc/on_quest_component_deleted(datum/source)
	SIGNAL_HANDLER
	return

/obj/item/parcel/attackby(obj/item/I, mob/user)
	if(istype(I, /obj/item/parcel) || I.w_class > WEIGHT_CLASS_BULKY || length(contained_items))
		to_chat(user, span_warning("You can't wrap this in [src]."))
		return

	if(do_after(user, 2 SECONDS, target = src))
		user.transferItemToLoc(I, src)
		contained_items += I
		name = "parcel ([I.name])"
		desc = "A securely wrapped parcel containing [I.name]."
		icon_state = I.w_class >= WEIGHT_CLASS_NORMAL ? "ration_large" : "ration_small"
		dropshrink = 1
		update_icon()
		playsound(get_turf(user), 'sound/foley/dropsound/food_drop.ogg', 40, TRUE, -1)
		to_chat(user, span_notice("You wrap [I] in the parcel wrapper."))

/obj/item/parcel/attack_self(mob/user)
	if(!length(contained_items))
		return

	if(delivery_area_type)
		var/area/quest_area = delivery_area_type
		if(ispath(quest_area, /area) && !(user.job in allowed_jobs))
			to_chat(user, span_warning("This parcel is sealed for delivery to [initial(quest_area.name)] and can only be opened by: [english_list(allowed_jobs)]!"))
			return FALSE

	if(!do_after(user, 2 SECONDS, target = src))
		return

	playsound(get_turf(user), 'sound/foley/dropsound/food_drop.ogg', 40, TRUE, -1)
	// Single-item parcels go into the opener's hand; multi-item dump on the floor.
	if(length(contained_items) == 1)
		var/obj/item/only = contained_items[1]
		to_chat(user, span_notice("You unwrap [only] from the parcel."))
		user.put_in_hands(only)
		only.update_icon()
	else
		to_chat(user, span_notice("You unwrap [src] and tip out the contents."))
		var/turf/drop_loc = get_turf(user)
		for(var/obj/item/I as anything in contained_items)
			I.forceMove(drop_loc)
			I.update_icon()
	contained_items.Cut()
	qdel(src)

/obj/item/parcel/examine(mob/user)
	. = ..()
	if(!delivery_area_type)
		return

	var/area/delivery_area = delivery_area_type
	if(!ispath(delivery_area, /area))
		return

	. += span_info("This parcel is addressed to [initial(delivery_area.name)].")
	. += (user.job in allowed_jobs) ? \
		span_notice("As [user.job], you're authorized to open this.") : \
		span_warning("It's sealed with an official guild mark - only authorized personnel should open this!")
