//armor parent obj
/datum/component/storage/concrete/roguetown/armor
	screen_max_rows = 2
	screen_max_columns = 2
	max_w_class = WEIGHT_CLASS_GIGANTIC

	cant_hold = list(
		/obj/item/storage,
		/obj/item/rogueweapon,
		/obj/item/bomb,
		/obj/item/flashlight,
		/obj/item/recipe_book,
	)

	attack_hand_interact = FALSE
	silent = TRUE
	rustle_sound = null

	insert_preposition = "onto"
	allow_big_nesting = TRUE
	allow_nesting = TRUE
	intercept_parent_attack = FALSE
	intercept_parent_mousedrop = FALSE

/datum/component/storage/concrete/roguetown/armor/attackby(datum/source, obj/item/attacking_item, mob/user, params, storage_click)
	if(is_type_in_list(attacking_item, cant_hold))
		return FALSE
	return ..()

/datum/component/storage/concrete/roguetown/armor/update_icon()
	. = ..()
	var/obj/our_parent = real_location()
	if(ismob(our_parent.loc))
		var/mob/parent_mob = our_parent.loc
		parent_mob.update_inv_armor()

/datum/component/storage/concrete/roguetown/armor/can_be_inserted(obj/item/storing, stop_messages, mob/user, worn_check = FALSE, params, storage_click = FALSE)
	// we only want aesthetically armor/cloak items, like tabards and shirts, to be addable
	if(!(storing.slot_flags & (ITEM_SLOT_CLOAK|ITEM_SLOT_ARMOR|ITEM_SLOT_SHIRT)))
		return FALSE
	// any sort of armoured item is forbidden, it's aesthetic only
	if(storing.armor?.stab > 0 || storing.armor?.blunt > 0)
		return FALSE
	// don't allow recursive nesting, it must be empty
	if(length(storing.contents))
		return FALSE
	return ..()

/obj/item/clothing/suit/roguetown/armor
	slot_flags = ITEM_SLOT_ARMOR
	body_parts_covered = CHEST
	body_parts_inherent = CHEST
	icon = 'icons/roguetown/clothing/armor.dmi'
	mob_overlay_icon = 'icons/roguetown/clothing/onmob/armor.dmi'
	equip_sound = 'sound/foley/equip/equip_armor.ogg'
	drop_sound = 'sound/foley/equip/equip_armor.ogg'
	pickup_sound =  'sound/blank.ogg'
	sleeved = 'icons/roguetown/clothing/onmob/helpers/sleeves_armor.dmi'
	sleevetype = "shirt"
	edelay_type = 0
	equip_delay_self = 2.5 SECONDS
	unequip_delay_self = 2.5 SECONDS
	bloody_icon_state = "bodyblood"
	boobed = TRUE
	resistance_flags = FIRE_PROOF
	blocksound = PLATEHIT
	break_sound = 'sound/foley/breaksound.ogg'
	drop_sound = 'sound/foley/dropsound/armor_drop.ogg'
	experimental_onhip = TRUE
	experimental_inhand = TRUE
	nodismemsleeves = TRUE
	flags_inv = HIDEBOOB|HIDECROTCH
	grid_width = 64
	grid_height = 96

	var/attachment_component = /datum/component/storage/concrete/roguetown/armor

/obj/item/clothing/suit/roguetown/armor/Initialize()
	. = ..()
	if(attachment_component)
		AddComponent(attachment_component)

/obj/item/clothing/suit/roguetown/armor/ComponentInitialize()
	AddComponent(/datum/component/armour_filtering/positive, TRAIT_FENCERDEXTERITY)

/obj/item/clothing/suit/roguetown/armor/get_examine_name(mob/user)
	var/default_examine_name = ..()
	if(attachment_component)
		var/datum/component/storage/concrete/roguetown/our_component = GetComponent(attachment_component)
		if(length(our_component.item_to_grid_coordinates))
			var/list/examine_strings = list()
			for(var/obj/item/thing as anything in our_component.item_to_grid_coordinates)
				examine_strings += thing.get_examine_name(user)
			default_examine_name += " ([examine_strings.Join(", ")])"
	return default_examine_name

/obj/item/clothing/suit/roguetown/armor/get_mechanics_examine(mob/user)
	. = ..()
	if(attachment_component)
		. += span_info("Shift + RMB will open aesthetic storage, allowing the user to layer extra decorations over \the [src].")
		. += span_info("Alt + RMB allows the user to toggle aesthetic storage (Shift + RMB) items on or off.")

/obj/item/clothing/suit/roguetown/armor/ShiftRightClick(mob/user)
	if(attachment_component)
		var/datum/component/storage/storage_component = GetComponent(attachment_component)
		if(storage_component)
			storage_component.rmb_show(user)
			return TRUE
	return ..()

/obj/item/clothing/suit/roguetown/armor/AltRightClick(mob/user)
	. = ..()
	if(!istype(loc, /mob/living/carbon))
		return
	if(attachment_component)
		var/datum/component/storage/concrete/roguetown/storage_component = GetComponent(attachment_component)
		if(storage_component && length(storage_component.item_to_grid_coordinates))
			var/list/options = list()
			for(var/obj/item/clothing/C in storage_component.item_to_grid_coordinates)
				if(!C || !isclothing(C))
					continue
				if(C.item_flags & NOT_SHOW_IN_STORAGE)
					options["[C.name] (Hidden)"] = C
				else
					options["[C.name] (Shown)"] = C
			var/choice = input(user, "Choose clothing to layer:","Layering") as null|anything in options
			if(choice)
				var/clothes_to_change = options[choice]
				if(isclothing(clothes_to_change))
					var/obj/item/clothing/C = clothes_to_change
					if(C.item_flags & NOT_SHOW_IN_STORAGE)
						C.item_flags &= ~NOT_SHOW_IN_STORAGE
					else
						C.item_flags |= NOT_SHOW_IN_STORAGE
					to_chat(user, span_info("[C] will be [(C.item_flags & NOT_SHOW_IN_STORAGE) ? "hidden" : "visible"] \the [src]"))
				user.update_inv_head()


/obj/item/clothing/suit/roguetown/armor/build_worn_icon(default_layer = 0, default_icon_file = null, isinhands = FALSE, femaleuniform = NO_FEMALE_UNIFORM, override_state = null, female = FALSE, customi = null, sleeveindex, boobed_overlay = FALSE, var/icon/clip_mask = null)
	var/mutable_appearance/standing = ..()
	// get attachment component and check if there's anything inside
	if(attachment_component)
		var/datum/component/storage/concrete/roguetown/our_component = GetComponent(attachment_component)
		if(our_component && length(our_component.item_to_grid_coordinates))
			for(var/obj/item/thing as anything in our_component.item_to_grid_coordinates)
				if(thing.item_flags & NOT_SHOW_IN_STORAGE)
					continue
				var/mutable_appearance/thing_appearance = thing.build_worn_icon(default_layer, default_icon_file, isinhands, femaleuniform, override_state, female, customi, sleeveindex, boobed_overlay, clip_mask)
				thing_appearance.appearance_flags = RESET_COLOR
				thing_appearance.pixel_x += standing.pixel_x
				thing_appearance.pixel_y += standing.pixel_y
				standing.add_overlay(thing_appearance)
	return standing
