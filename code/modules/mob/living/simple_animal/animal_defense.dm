/mob/living/simple_animal/attacked_by(obj/item/I, mob/living/user)
	if(I.force_dynamic < force_threshold || I.damtype == STAMINA)
		playsound(loc, 'sound/blank.ogg', I.get_clamped_volume(), TRUE, -1)
	else
		var/hitlim = simple_limb_hit(user.zone_selected)
		I.funny_attack_effects(src, user)
		if(I.force_dynamic)
			var/newforce = get_complex_damage(I, user)
			var/haha = user.used_intent.blade_class
			var/armor = run_armor_check(null, haha, armor_penetration = I.armor_penetration, damage = newforce, used_weapon = I)
			var/nodmg = FALSE
			next_attack_msg.Cut()
			if(armor > 0)
				nodmg = TRUE
				next_attack_msg += VISMSG_ARMOR_BLOCKED
			apply_damage(newforce, I.damtype, hitlim, armor)
			I.remove_bintegrity(1)
			if(I.damtype == BRUTE && !nodmg)
				if(HAS_TRAIT(src, TRAIT_SIMPLE_WOUNDS))
					if(I.is_silver && HAS_TRAIT(src, TRAIT_SILVER_WEAK))
						newforce *= SILVER_SIMPLEMOB_DAM_MULT
					simple_woundcritroll(user.used_intent.blade_class, newforce, user, hitlim)
				if(newforce > 5)
					if(haha != BCLASS_BLUNT)
						I.add_mob_blood(src)
						var/turf/location = get_turf(src)
						add_splatter_floor(location)
						add_splatter_wall(location, force = newforce)
						if(get_dist(user, src) <= 1)	//people with TK won't get smeared with blood
							user.add_mob_blood(src)
				if(newforce > 15)
					if(haha == BCLASS_BLUNT)
						I.add_mob_blood(src)
						var/turf/location = get_turf(src)
						add_splatter_floor(location)
						add_splatter_wall(location, force = newforce)
						if(get_dist(user, src) <= 1)	//people with TK won't get smeared with blood
							user.add_mob_blood(src)
		send_item_attack_message(I, user, hitlim)
		next_attack_msg.Cut()
		if(I.force_dynamic)
			return TRUE
		I.do_special_attack_effect(user, null, null, src, null)

/mob/living/simple_animal/getarmor(def_zone, type, damage, armor_penetration, blade_dulling, peeldivisor, intdamfactor = 1, used_weapon)
	if(!type)
		return 0
	var/armorval = 0
	if(bbarding && !bbarding.obj_broken)
		armorval = bbarding.armor.getRating(type)
		var/intdamage = damage
		if(type != "blunt")
			if((damage + armor_penetration) > armorval)
				intdamage = (damage + armor_penetration) - armorval

			if(intdamfactor != 1)
				intdamage *= intdamfactor

			bbarding.take_damage(intdamage, damage_flag = type, sound_effect = FALSE, armor_penetration = 100)
		else
			if(mind)
				if(armorval > 0)
					intdamage -= intdamage * ((armorval / 1.66) / 100)	//Reduces it up to 60% (100 dmg -> 40 dmg at Blunt S armor (100))
			if(intdamfactor != 1)
				intdamage *= intdamfactor

			bbarding.take_damage(intdamage, damage_flag = type, sound_effect = FALSE, armor_penetration = 100)

	return armorval

/mob/living/simple_animal/damage_clothes(damage_amount, damage_type = BRUTE, damage_flag = 0, def_zone)
	if(damage_type != BRUTE && damage_type != BURN)
		return
	if(!bbarding)
		return
	damage_amount *= 0.5 //0.5 multiplier for balance reason, we don't want clothes to be too easily destroyed
	bbarding.take_damage(damage_amount, damage_type, damage_flag, 0)

/mob/living/simple_animal/attack_hand(mob/living/carbon/human/M)
	..()
	switch(M.used_intent.type)
		if(INTENT_HELP)
			if (health > 0)
				visible_message(span_notice("[M] [response_help_continuous] [src]."), \
								span_notice("[M] [response_help_continuous] you."), null, null, M)
				to_chat(M, span_notice("I [response_help_simple] [src]."))
				playsound(loc, 'sound/blank.ogg', 50, TRUE, -1)
			return TRUE

		if(INTENT_GRAB)
			if(!M.has_hand_for_held_index(M.active_hand_index, TRUE)) //we obviously have a hadn, but we need to check for fingers/prosthetics
				to_chat(M, span_warning("I can't move the fingers."))
				return
			grabbedby(M)
			return TRUE

		if(INTENT_HARM)
			var/atk_verb = pick(M.used_intent.attack_verb)
			if(HAS_TRAIT(M, TRAIT_PACIFISM))
				to_chat(M, span_warning("I don't want to hurt [src]!"))
				return
			M.do_attack_animation(src, M.used_intent.animname)
			playsound(loc, attacked_sound, 25, TRUE, -1)
			var/damage = M.get_punch_dmg()
			next_attack_msg.Cut()
			var/hitlim = simple_limb_hit(M.zone_selected)
			var/haha = M.used_intent.item_d_type
			var/armor = run_armor_check(null, haha, armor_penetration = M.used_intent.penfactor, damage = damage)
			if(armor > 0)
				next_attack_msg += VISMSG_ARMOR_BLOCKED
			attack_threshold_check(damage, hitlim, armorcheck = armor)
			log_combat(M, src, "attacked")
			updatehealth()
			simple_woundcritroll(M.used_intent.blade_class, damage, M, hitlim)
			visible_message(span_danger("[M] [atk_verb] [src]![next_attack_msg.Join()]"),\
							span_danger("[M] [atk_verb] me![next_attack_msg.Join()]"), null, COMBAT_MESSAGE_RANGE)
			next_attack_msg.Cut()
			return TRUE

		if(INTENT_DISARM)
			var/mob/living/carbon/human/user = M
			var/mob/living/simple_animal/target = src
			if(!(user.mobility_flags & MOBILITY_STAND) || user.IsKnockdown())
				return FALSE
			if(user == target)
				return FALSE
			if(user.loc == target.loc)
				return FALSE
			else
				user.do_attack_animation(target, ATTACK_EFFECT_DISARM)
				playsound(target, 'sound/combat/shove.ogg', 100, TRUE, -1)

				var/turf/target_oldturf = target.loc
				var/shove_dir = get_dir(user.loc, target_oldturf)
				var/turf/target_shove_turf = get_step(target.loc, shove_dir)
				var/mob/living/target_collateral_mob
				var/obj/structure/table/target_table
				var/shove_blocked = FALSE //Used to check if a shove is blocked so that if it is knockdown logic can be applied
				if(prob(30 + generic_stat_comparison(user.STASTR, target.STACON) ))//check if we actually shove them
					target_collateral_mob = locate(/mob/living) in target_shove_turf.contents
					if(target_collateral_mob)
						shove_blocked = TRUE
					else
						target.Move(target_shove_turf, shove_dir)
						if(get_turf(target) == target_oldturf)
							target_table = locate(/obj/structure/table) in target_shove_turf.contents
							if(target_table)
								shove_blocked = TRUE

				if(shove_blocked && !target.buckled)
					var/directional_blocked = FALSE
					if(shove_dir in GLOB.cardinals) //Directional checks to make sure that we're not shoving through a windoor or something like that
						var/target_turf = get_turf(target)
						for(var/obj/O in target_turf)
							if(O.flags_1 & ON_BORDER_1 && O.dir == shove_dir && O.density)
								directional_blocked = TRUE
								break
						if(target_turf != target_shove_turf) //Make sure that we don't run the exact same check twice on the same tile
							for(var/obj/O in target_shove_turf)
								if(O.flags_1 & ON_BORDER_1 && O.dir == turn(shove_dir, 180) && O.density)
									directional_blocked = TRUE
									break
					if((!target_table && !target_collateral_mob) || directional_blocked)
						target.Stun(10)
						target.visible_message(span_danger("[user.name] shoves [target.name]!"),
										span_danger("I'm shoved by [user.name]!"), span_hear("I hear aggressive shuffling followed by a loud thud!"), COMBAT_MESSAGE_RANGE, user)
						to_chat(user, span_danger("I shove [target.name]!"))
						log_combat(user, target, "shoved", "knocking them down")
					else if(target_table)
						target.Stun(10)
						target.visible_message(span_danger("[user.name] shoves [target.name] onto \the [target_table]!"),
										span_danger("I'm shoved onto \the [target_table] by [user.name]!"), span_hear("I hear aggressive shuffling followed by a loud thud!"), COMBAT_MESSAGE_RANGE, user)
						to_chat(user, span_danger("I shove [target.name] onto \the [target_table]!"))
						target.throw_at(target_table, 1, 1, null, FALSE) //1 speed throws with no spin are basically just forcemoves with a hard collision check
						log_combat(user, target, "shoved", "onto [target_table] (table)")
					else if(target_collateral_mob)
						target.Stun(10)
						target_collateral_mob.Stun(SHOVE_KNOCKDOWN_COLLATERAL)
						target.visible_message(span_danger("[user.name] shoves [target.name] into [target_collateral_mob.name]!"),
							span_danger("I'm shoved into [target_collateral_mob.name] by [user.name]!"), span_hear("I hear aggressive shuffling followed by a loud thud!"), COMBAT_MESSAGE_RANGE, user)
						to_chat(user, span_danger("I shove [target.name] into [target_collateral_mob.name]!"))
						log_combat(user, target, "shoved", "into [target_collateral_mob.name]")
				else
					target.visible_message(span_danger("[user.name] shoves [target.name]!"),
									span_danger("I'm shoved by [user.name]!"), span_hear("I hear aggressive shuffling!"), COMBAT_MESSAGE_RANGE, user)
					to_chat(user, span_danger("I shove [target.name]!"))
					log_combat(user, target, "shoved")
			return TRUE

	if(M.used_intent.unarmed)
		var/atk_verb = pick(M.used_intent.attack_verb)
		if(HAS_TRAIT(M, TRAIT_PACIFISM))
			to_chat(M, span_warning("I don't want to hurt [src]!"))
			return
		M.do_attack_animation(src, M.used_intent.animname)
		playsound(loc, attacked_sound, 25, TRUE, -1)
		var/damage = M.get_punch_dmg()
		next_attack_msg.Cut()
		var/hitlim = simple_limb_hit(M.zone_selected)
		var/haha = M.used_intent.item_d_type
		var/armor = run_armor_check(null, haha, armor_penetration = M.used_intent.penfactor, damage = damage)
		if(armor > 0)
			next_attack_msg += VISMSG_ARMOR_BLOCKED
		attack_threshold_check(damage, hitlim, armorcheck = armor)
		log_combat(M, src, "attacked")
		updatehealth()
		simple_woundcritroll(M.used_intent.blade_class, damage, M, hitlim)
		visible_message(span_danger("[M] [atk_verb] [src]![next_attack_msg.Join()]"),\
						span_danger("[M] [atk_verb] me![next_attack_msg.Join()]"), null, COMBAT_MESSAGE_RANGE)
		next_attack_msg.Cut()
		return TRUE

/mob/living/simple_animal/attack_paw(mob/living/carbon/monkey/M)
	if(..()) //successful monkey bite.
		if(stat != DEAD)
			var/damage = rand(1, 3)
			var/hitlim = simple_limb_hit(M.zone_selected)
			var/haha = M.used_intent.item_d_type
			var/armor = run_armor_check(null, haha, armor_penetration = M.used_intent.penfactor, damage = damage)
			attack_threshold_check(damage, hitlim, armorcheck = armor)
	if (M.used_intent.type == INTENT_HELP)
		if (health > 0)
			visible_message(span_notice("[M.name] [response_help_continuous] [src]."), \
							span_notice("[M.name] [response_help_continuous] you."), null, COMBAT_MESSAGE_RANGE, M)
			to_chat(M, span_notice("I [response_help_simple] [src]."))
			playsound(loc, 'sound/blank.ogg', 50, TRUE, -1)


/mob/living/simple_animal/attack_animal(mob/living/simple_animal/M)
	. = ..()
	if(.)
		next_attack_msg.Cut()
		var/hitlim = simple_limb_hit(M.zone_selected)
		var/damage = rand(M.melee_damage_lower, M.melee_damage_upper)
		var/haha = M.d_type
		var/armor = run_armor_check(null, haha, armor_penetration = M.armor_penetration, damage = damage)
		if(armor > 0)
			next_attack_msg += VISMSG_ARMOR_BLOCKED
		attack_threshold_check(damage, hitlim, M.melee_damage_type, armor)
		simple_woundcritroll(M.a_intent.blade_class, damage, M, hitlim)
		visible_message(span_danger("\The [M] [pick(M.a_intent.attack_verb)] [src]![next_attack_msg.Join()]"), \
					span_danger("\The [M] [pick(M.a_intent.attack_verb)] me![next_attack_msg.Join()]"), null, COMBAT_MESSAGE_RANGE)
		next_attack_msg.Cut()

/mob/living/simple_animal/onbite(mob/living/carbon/human/user)
	var/damage = 10*(user.STASTR/20)
	if(HAS_TRAIT(user, TRAIT_STRONGBITE))
		damage = damage*2
	playsound(user.loc, "smallslash", 100, FALSE, -1)
	user.next_attack_msg.Cut()
	if(stat == DEAD)
		if(user.has_status_effect(/datum/status_effect/fire_handler/fire_stacks/sunder))
			to_chat(user, span_notice("My power is weakened, I cannot heal!"))
			return
		if(user.mind && istype(user, /mob/living/carbon/human/species/werewolf))
			visible_message(span_danger("The werewolf ravenously consumes the [src]!"))
			to_chat(src, span_warning("I feed on succulent flesh. I feel reinvigorated."))
			user.reagents.add_reagent(/datum/reagent/medicine/healthpot, 30)
			gib()
		if(user.mind && istype(user, /mob/living/carbon/human/species/wildshape/volf))
			visible_message(span_danger("The volf ravenously consumes the [src]!"))
			to_chat(src, span_warning("I feed on succulent flesh. I feel satiated."))
			user.reagents.add_reagent(/datum/reagent/consumable/nutriment, 15)
			gib()
		if(loc == user.loc)
			var/datum/antagonist/vampire/vamp_biter = user.mind.has_antag_datum(/datum/antagonist/vampire)
			if(vamp_biter)
				var/mob/living/vampire_victim = src
				var/bloodleft = vampire_victim.blood_volume
				if (bloodleft < 100)
					visible_message(span_danger("[user] bites the [vampire_victim]!"))
					to_chat(user, span_warning("There's not enough blood left for me"))
				else
					user.visible_message(span_warning("[user] drinks from [vampire_victim]!"),\
					span_warning("I drink from [vampire_victim]!"))
					playsound(user.loc, 'sound/misc/drink_blood.ogg', 100, FALSE, -4)
					vampire_victim.blood_volume -= 100
					if(bloodleft < 100)
						vampire_victim.blood_volume = 0
					user.adjust_bloodpool(100)
					user.add_stress(/datum/stressevent/drankrat)
				return
		return
	var/hitlim = simple_limb_hit(user.zone_selected)
	var/armor = run_armor_check(null, "stab", armor_penetration = 0, damage = damage)
	if(armor > 0)
		user.next_attack_msg += VISMSG_ARMOR_BLOCKED
	if(src.apply_damage(damage, BRUTE, hitlim, armor))
		if(istype(user, /mob/living/carbon/human/species/werewolf))
			visible_message(span_danger("The werewolf bites into [src] and thrashes!"))
		else
			visible_message(span_danger("[user] bites [src]! What is wrong with them?"))
	user.next_attack_msg.Cut()

/mob/living/simple_animal/onkick(mob/M)
	var/mob/living/simple_animal/target = src
	var/mob/living/carbon/human/user = M
	if(HAS_TRAIT(user, TRAIT_PACIFISM))
		to_chat(user, span_warning("I don't want to harm [target]!"))
		return FALSE
	if(user.IsKnockdown())
		return FALSE
	if(user == target)
		return FALSE
	if(!HAS_TRAIT(user, TRAIT_GARROTED))	
		if(user.check_leg_grabbed(1) || user.check_leg_grabbed(2))
			to_chat(user, span_notice("I can't move my leg!"))
			return
	if(user.stamina >= user.max_stamina)
		return FALSE
	if(user.loc == target.loc)
		to_chat(user, span_warning("I'm too close to get a good kick in."))
		return FALSE
	else
		user.do_attack_animation(target, ATTACK_EFFECT_DISARM)
		playsound(target, 'sound/combat/hits/kick/kick.ogg', 100, TRUE, -1)

		var/shove_dir = get_dir(user.loc, target.loc)
		var/turf/target_shove_turf = get_step(target.loc, shove_dir)

		target.Move(target_shove_turf, shove_dir)

		target.visible_message(span_danger("[user.name] kicks [target.name]!"),
						span_danger("I'm kicked by [user.name]!"), span_hear("I hear aggressive shuffling!"), COMBAT_MESSAGE_RANGE, user)
		to_chat(user, span_danger("I kick [target.name]!"))
		log_combat(user, target, "kicked")
		playsound(target, 'sound/combat/hits/kick/kick.ogg', 100, TRUE, -1)
		target.lastattacker = user.real_name
		target.lastattackerckey = user.ckey
		target.lastattacker_weakref = WEAKREF(user)
		if(target.mind)
			target.mind.attackedme[user.real_name] = world.time
		user.stamina_add(15)

/mob/living/simple_animal/proc/attack_threshold_check(damage, bodypart = null, damagetype = BRUTE, armorcheck = 0)
	var/temp_damage = damage
	if(!damage_coeff[damagetype])
		temp_damage = 0
	else
		temp_damage *= damage_coeff[damagetype]

	if(temp_damage >= 0 && temp_damage <= force_threshold)
		visible_message(span_warning("[src] looks unharmed!"))
		return FALSE
	else
		apply_damage(damage, damagetype, bodypart, armorcheck)
		return TRUE

/mob/living/simple_animal/ex_act(severity, target, epicenter, devastation_range, heavy_impact_range, light_impact_range, flame_range)
	..()
	if(!severity || !epicenter)
		return
	var/ddist = devastation_range || 0
	var/hdist = heavy_impact_range || 0
	var/ldist = light_impact_range || 0
	var/fdist = flame_range || 0
	var/fodist = get_dist(src, epicenter)
	var/brute_loss = 0
	var/burn_loss = 0
	var/dmgmod = round(rand(0.5, 1.5), 0.1)

	if(fdist)
		var/stacks = ((fdist - fodist) * 2)
		fire_act(stacks)

	switch(severity)
		if(EXPLODE_DEVASTATE)
			brute_loss = ((120 * ddist) - (120 * fodist) * dmgmod)
			burn_loss = ((60 * ddist) - (60 * fodist) * dmgmod)
			Unconscious((50 * ddist) - (15 * fodist))
			Knockdown((30 * ddist) - (30 * fodist))

		if(EXPLODE_HEAVY)
			brute_loss = ((40 * hdist) - (40 * fodist) * dmgmod)
			burn_loss = ((20 * hdist) - (20 * fodist) * dmgmod)
			Unconscious((10 * hdist) - (5 * fodist))
			Knockdown((30 * hdist) - (30 * fodist))

		if(EXPLODE_LIGHT)
			brute_loss = ((10 * ldist) - (10 * fodist) * dmgmod)

	take_overall_damage(brute_loss,burn_loss)

/mob/living/simple_animal/do_attack_animation(atom/A, visual_effect_icon, used_item, no_effect, used_intent = null, simplified = TRUE)
	if(!no_effect && !visual_effect_icon && melee_damage_upper)
		if(melee_damage_upper < 10)
			visual_effect_icon = ATTACK_EFFECT_PUNCH
		else
			visual_effect_icon = ATTACK_EFFECT_SMASH
	..()
